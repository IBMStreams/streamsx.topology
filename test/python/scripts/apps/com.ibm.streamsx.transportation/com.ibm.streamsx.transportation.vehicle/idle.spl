// ************************************************************************
// * Copyright (C)2014,2018, International Business Machines Corporation and
// * others. All Rights Reserved.
// ************************************************************************

namespace com.ibm.streamsx.transportation.vehicle;

use com.ibm.streamsx.datetime::TimeMillis;

/**
 * Output type from [IdleVehicleDetection].
 * 
 * * idle - Is the vehicle idle
 * * duration - Number of seconds the vehicle has been idle
 * * geohash - Geohash in base 32 the vehicle is in if `idle` is true, otherwise an empty string.
 */
type VehicleIdle = tuple<boolean idle, uint64 duration, rstring geohash>;

/**
 * Detect vehicles that have not moved for a time period.
 * Augments the input tuple type with [VehicleIdle], any
 * input attributes are copied into the output tuple.
 * 
 * This is a wrapper around `com.ibm.streams.geospatial::Hangout` with
 * defaults suitable for a vehicle. The default settings match to the vehicle
 * being idle if it is in an 75mx75m (approximately) box.
 * 
 * For details on how to configure see the documentation for `com.ibm.streams.geospatial::Hangout`.
 * 
 * @param minimumIdle Time a vehicle must within a region to be seen as idle in seconds. 
 * @param $geohashCellSize The extent of each Geohash region, in meters. Default is 75.0, which corresponds to geohash cells of roughly 75m x 75m in size.
 * @param $sampleLatitude  Value indicating the latitude of the general area where the data is coming from. Default is 37, which corresponds to the San Fransisco area.
 * @param precision Specifies the distance, in meters, by which the computed boundaries of each Geohash region will be extended on all sides. Defaults to 25m.
 * @param timeoutFactor Specifies the timeout as a factor of `minimumIdle`. If no tuples have arrived for 
 * 
 * @input VehicleLocations Vehicle locations, must be a schema that extends [VehicleLocation].
 * @output VehicleLocationsIdle Input tuples augmented with idle information, the schema is the input schema augmented with [VehicleIdle].
 */
public composite IdleVehicleDetection(input VehicleLocations; output VehicleLocationsIdle) {
    param
       expression<float64> $minimumIdle;
       expression<float64> $sampleLatitude: 37.0;
       
       expression<float64> $geohashCellSize : 75.0;
       
       expression<float64> $precision : 25.0;
       expression<uint32> $timeoutFactor : 10u;
    graph
     
   stream<VehicleLocations, VehicleIdle> VehicleLocationsIdle = com.ibm.streams.geospatial::Hangout(VehicleLocations) {
      param
         minimumDwellTime: (uint32) $minimumIdle;
         cellSize: $geohashCellSize;
         precision: $precision;
         timeStamp: reportTime;
         sampleLatitude: $sampleLatitude;
         timeout: (uint32) ($minimumIdle * (float64) $timeoutFactor);
      output VehicleLocationsIdle:
         idle = IsInHangout(),
         duration = HangoutDuration(),
         geohash = HangoutGeohashBase32();
   } 
}

